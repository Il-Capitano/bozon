// Copyright 2018 Ulf Adams
//
// The contents of this file may be used under the terms of the Apache License,
// Version 2.0.
//
//    (See accompanying file LICENSE-Apache or copy at
//     http://www.apache.org/licenses/LICENSE-2.0)
//
// Alternatively, the contents of this file may be used under the terms of
// the Boost Software License, Version 1.0.
//    (See accompanying file LICENSE-Boost or copy at
//     https://www.boost.org/LICENSE_1_0.txt)
//
// Unless required by applicable law or agreed to in writing, this software
// is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.

import std::utils;

// Returns the number of decimal digits in v, which must not contain more than 9 digits.
export function decimalLength9(v: uint32) -> uint32
{
	// Function precondition: v is not a 10-digit number.
	// (f2s: 9 digits are sufficient for round-tripping.)
	// (d2fixed: We print 9-digit blocks.)
	assert(v < 1'000'000'000);
	return if (v >= 100'000'000) { 9 }
		else if (v >= 10'000'000) { 8 }
		else if (v >=  1'000'000) { 7 }
		else if (v >=    100'000) { 6 }
		else if (v >=     10'000) { 5 }
		else if (v >=      1'000) { 4 }
		else if (v >=        100) { 3 }
		else if (v >=         10) { 2 }
		else                      { 1 };
}

// Returns e == 0 ? 1 : [log_2(5^e)]; requires 0 <= e <= 3528.
export function log2pow5(e: uint32) -> uint32
{
	// This approximation works up to the point that the multiplication overflows at e = 3529.
	// If the multiplication were done in 64 bits, it would fail at 5^4004 which is just greater
	// than 2^9297.
	assert(e >= 0);
	assert(e <= 3528);
	return (e * 1217359) >> 19;
}

// Returns e == 0 ? 1 : ceil(log_2(5^e)); requires 0 <= e <= 3528.
export function pow5bits(e: uint32) -> uint32
{
	// This approximation works up to the point that the multiplication overflows at e = 3529.
	// If the multiplication were done in 64 bits, it would fail at 5^4004 which is just greater
	// than 2^9297.
	assert(e >= 0);
	assert(e <= 3528);
	return ((e * 1217359) >> 19) + 1;
}

// Returns e == 0 ? 1 : ceil(log_2(5^e)); requires 0 <= e <= 3528.
export function ceil_log2pow5(e: uint32) -> uint32
{
	return log2pow5(e) + 1;
}

// Returns floor(log_10(2^e)); requires 0 <= e <= 1650.
export function log10Pow2(e: uint32) -> uint32
{
	// The first value this approximation fails for is 2^1651 which is just greater than 10^297.
	assert(e >= 0);
	assert(e <= 1650);
	return (e * 78913) >> 18;
}

// Returns floor(log_10(5^e)); requires 0 <= e <= 2620.
export function log10Pow5(e: uint32) -> uint32
{
	// The first value this approximation fails for is 5^2621 which is just greater than 10^1832.
	assert(e >= 0);
	assert(e <= 2620);
	return (e * 732923) >> 20;
}

export function copy_special_str(result: *uint8, sign: bool, exponent: bool, mantissa: bool) -> int32
{
	if (mantissa)
	{
		trivially_copy_values(result, "NaN".begin_ptr(), 3);
		return 3;
	}
	if (sign)
	{
		*result = '-' as uint8;
	}
	if (exponent)
	{
		trivially_copy_values(if (sign) { result + 1 } else { result }, "Infinity".begin_ptr(), 8);
		return if (sign) { 9 } else { 8 };
	}
	trivially_copy_values(if (sign) { result + 1 } else { result }, "0E0".begin_ptr(), 3);
	return if (sign) { 4 } else { 3 };
}

export function float_to_bits(f: float32) -> uint32
{
	return bit_cast(uint32, f);
}

export function double_to_bits(d: float64) -> uint64
{
	return bit_cast(uint64, d);
}


//
// addition by me
//

// writes nan, inf or 0.0 to output with sign
export function copy_short_special_str(result: *uint8, sign: bool, exponent: bool, mantissa: bool) -> *uint8
{
	if (mantissa)
	{
		trivially_copy_values(result, "nan".begin_ptr(), 3);
		return result + 3;
	}

	if (sign)
	{
		*result = '-' as uint8;
		++result;
	}
	if (exponent)
	{
		trivially_copy_values(result, "inf".begin_ptr(), 3);
		return result + 3;
	}
	else
	{
		trivially_copy_values(result, "0.0".begin_ptr(), 3);
		return result + 3;
	}
}

export function bits_to_double(bits: uint64) -> float64
{
	return bit_cast(float64, bits);
}

export enum parse_result
{
	SUCCESS         = 0,
	INPUT_TOO_SHORT = 1,
	INPUT_TOO_LONG  = 2,
	MALFORMED_INPUT = 3,
}
