import unicode;
import utils;
import bit;
import meta::comptime;

static_assert(sizeof string == 3 * sizeof ?*u8);
consteval short_string_max_size = 2 * sizeof ?*u8;

export struct string
{
	._data_begin: ?*mut u8;
	._data_end:   ?*mut u8;
	._alloc_end:  ?*mut u8;

	destructor(&self)
	{
		if (!self.is_short_string())
		{
			free(self._data_begin);
		}
	}

	constructor()
	{
		return string[ null, null, null ];
	}

	constructor(s: str)
	{
		let size = s.size();
		if (size == 0)
		{
			return string[ null, null, null ];
		}
		else if (!meta::is_comptime() && size <= short_string_max_size)
		{
			let data_begin = __builtin_int_to_pointer(*mut u8, 1uz | (size << 1));
			mut data_and_alloc_end = [2: ?*mut u8]();
			memcpy(&data_and_alloc_end, s.begin_ptr(), size);
			return string[ data_begin, data_and_alloc_end[0], data_and_alloc_end[1] ];
		}
		else
		{
			let [data_begin, alloc_size] = rounded_alloc(size);
			trivially_copy_values(data_begin, s.begin_ptr(), size);
			return string[ data_begin, data_begin.get_value() + size, data_begin.get_value() + alloc_size ];
		}
	}

	constructor(other: &string)
	{
		return string(other.as_str());
	}

	export operator = (self: &mut string, other: &string) -> &mut string
	{
		if (&self == &other)
		{
			return self;
		}
		let size = other.size();
		self.reserve(size);
		trivially_copy_values(self.begin_ptr(), other.begin_ptr(), size);
		self.set_size_unsafe(size);
		return self;
	}

	export operator = (self: &mut string, other: string) = __default__;

	export operator = (self: &mut string, rhs: str) -> &mut string
	{
		let size = rhs.size();
		if (self.capacity() >= size)
		{
			trivially_copy_values(self.begin_ptr(), rhs.begin_ptr(), size);
			self.set_size_unsafe(size);
		}
		else
		{
			self = string(rhs);
		}
		return self;
	}
}

function round_to_next_power_of_two(n: usize) -> usize
{
	return if (n <= 1) {
		1
	} else {
		1uz << (sizeof usize * 8 - clz(n - 1))
	};
}

function rounded_alloc(n: usize) -> [?*mut u8, usize]
{
	if (n == 0)
	{
		return [ null, 0 ];
	}
	else
	{
		let rounded_n = round_to_next_power_of_two(n);
		return [ malloc_raw(u8, rounded_n), rounded_n ];
	}
}

function is_short_string(s: &string) -> bool
{
	if (meta::is_comptime())
	{
		return false;
	}
	else
	{
		let data_begin_int = __builtin_pointer_to_int(s._data_begin);
		return s._data_begin == null || (data_begin_int & 1uz) != 0;
	}
}

function short_string_size(s: &string) -> usize
{
	let data_begin_int = __builtin_pointer_to_int(s._data_begin);
	return data_begin_int >> 1;
}

function begin_ptr(s: &string) -> ?*u8
{
	if (s.is_short_string())
	{
		return __builtin_pointer_cast(*u8, &s._data_end);
	}
	else
	{
		return s._data_begin;
	}
}

function begin_ptr(s: &mut string) -> ?*mut u8
{
	if (s.is_short_string())
	{
		return __builtin_pointer_cast(*mut u8, &s._data_end);
	}
	else
	{
		return s._data_begin;
	}
}

function end_ptr(s: &string) -> ?*u8
{
	if (s.is_short_string())
	{
		let size = s.short_string_size();
		return __builtin_pointer_cast(*u8, &s._data_end) + size;
	}
	else
	{
		return s._data_end;
	}
}

function end_ptr(s: &mut string) -> ?*mut u8
{
	if (s.is_short_string())
	{
		let size = s.short_string_size();
		return __builtin_pointer_cast(*mut u8, &s._data_end) + size;
	}
	else
	{
		return s._data_end;
	}
}

function set_short_string_size(s: &mut string, size: usize)
{
	if (size == 0)
	{
		s._data_begin = null;
	}
	else
	{
		s._data_begin = __builtin_int_to_pointer(*mut u8, (size << 1) | 1uz);
	}
}

function set_size_unsafe(s: &mut string, size: usize)
{
	if (s.is_short_string())
	{
		s.set_short_string_size(size);
	}
	else
	{
		s._data_end = s._data_begin + size;
	}
}


function as_slice(s: &string) -> [: u8]
{
	return __builtin_slice_from_ptrs(s.begin_ptr(), s.end_ptr());
}

export function as_str(s: &string) -> str
{
	return __builtin_str_from_ptrs(s.begin_ptr(), s.end_ptr());
}

export function as_str(s: move string) -> str
{
	return __builtin_str_from_ptrs(s.begin_ptr(), s.end_ptr());
}

export function length(s: #string) -> usize
{
	return s.as_str().length();
}

export function size(s: #string) -> usize
{
	return s.as_str().size();
}

export function capacity(self: #string) -> usize
{
	return if (self.is_short_string()) { short_string_max_size } else { (self._alloc_end - self._data_begin) as usize };
}

export function empty(self: #string) -> bool
{
	return self._data_begin == null | self._data_begin == self._data_end;
}

export function not_empty(self: #string) -> bool
{
	return !self.empty();
}

export function is_ascii(self: #string) -> bool
{
	for (let c in self.as_slice())
	{
		if ((c & 0b1000'0000u8) != 0)
		{
			return false;
		}
	}
	return true;
}

export function data(self: &string) -> ?*u8
{
	return self.begin_ptr();
}

export function data(self: &mut string) -> ?*mut u8
{
	return self.begin_ptr();
}

export function data_end(self: &string) -> ?*u8
{
	return self.end_ptr();
}

export function data_end(self: &mut string) -> ?*mut u8
{
	return self.end_ptr();
}


export function reserve(self: &mut string, new_capacity: usize)
{
	if (new_capacity > self.capacity())
	{
		let [data_begin, alloc_size] = rounded_alloc(new_capacity);
		let size = self.size();
		trivially_copy_values(data_begin, self.begin_ptr(), size);
		if (!self.is_short_string())
		{
			free(self._data_begin);
		}
		self._data_begin = data_begin;
		self._data_end   = data_begin + size;
		self._alloc_end  = data_begin + alloc_size;
	}
}

export function clear(self: &mut string)
{
	self.set_size_unsafe(0);
}

export function push_back(self: &mut string, c: char)
{
	let char_and_size = encode_char_utf8(c);
	self.reserve(self.size() + char_and_size[1]);
	trivially_copy_values(self.end_ptr(), char_and_size[0].begin(), char_and_size[1]);
	self.set_size_unsafe(self.size() + char_and_size[1]);
}

export function resize(self: &mut string, new_size: usize, c: char)
{
	assert(c <= '\x7f');
	if (new_size <= self.size())
	{
		self.set_size_unsafe(new_size);
	}
	else if (new_size <= self.capacity())
	{
		let old_end = self.end_ptr();
		let size_diff = new_size - self.size();
		self.set_size_unsafe(new_size);
		trivially_set_values(old_end, c as u8, size_diff);
	}
	else
	{
		self.reserve(new_size);
		let old_end = self.end_ptr();
		let size_diff = new_size - self.size();
		self.set_size_unsafe(new_size);
		trivially_set_values(old_end, c as u8, size_diff);
	}
}

export function resize(self: &mut string, new_size: usize)
{
	self.resize(new_size, '\x00');
}

export function release(s: &mut string) -> string
{
	let data_begin = s._data_begin;
	let data_end   = s._data_end;
	let alloc_end  = s._alloc_end;
	s._data_begin = null;
	s._data_end   = null;
	s._alloc_end  = null;
	return string[ data_begin, data_end, alloc_end ];
}


export operator + (lhs: #string, rhs: #string) -> string
{
	let lhs_begin = lhs.begin_ptr();
	let lhs_size = lhs.size();
	let rhs_begin = rhs.begin_ptr();
	let rhs_size = rhs.size();

	let result_size = lhs_size + rhs_size;

	let [result_alloc, result_alloc_size] = rounded_alloc(result_size);

	trivially_copy_values(result_alloc, lhs_begin, lhs_size);
	trivially_copy_values(result_alloc + lhs_size, rhs_begin, rhs_size);

	return string[ result_alloc, result_alloc + result_size, result_alloc + result_alloc_size ];
}

export operator + (lhs: #string, rhs: str) -> string
{
	let lhs_begin = lhs.begin_ptr();
	let lhs_size = lhs.size();
	let rhs_begin = rhs.begin_ptr();
	let rhs_size = rhs.size();

	let result_size = lhs_size + rhs_size;

	let [result_alloc, result_alloc_size] = rounded_alloc(result_size);

	trivially_copy_values(result_alloc, lhs_begin, lhs_size);
	trivially_copy_values(result_alloc + lhs_size, rhs_begin, rhs_size);

	return string[ result_alloc, result_alloc + result_size, result_alloc + result_alloc_size ];
}

export operator + (lhs: str, rhs: #string) -> string
{
	let lhs_begin = lhs.begin_ptr();
	let lhs_size = lhs.size();
	let rhs_begin = rhs.begin_ptr();
	let rhs_size = rhs.size();

	let result_size = lhs_size + rhs_size;

	let [result_alloc, result_alloc_size] = rounded_alloc(result_size);

	trivially_copy_values(result_alloc, lhs_begin, lhs_size);
	trivially_copy_values(result_alloc + lhs_size, rhs_begin, rhs_size);

	return string[ result_alloc, result_alloc + result_size, result_alloc + result_alloc_size ];
}

export operator + (lhs: #string, rhs: char) -> string
{
	let lhs_begin = lhs.begin_ptr();
	let lhs_size = lhs.size();
	let rhs_encoded = encode_char_utf8(rhs);
	let rhs_begin = rhs_encoded[0].begin();
	let rhs_size = rhs_encoded[1] as usize;

	let result_size = lhs_size + rhs_size;

	let [result_alloc, result_alloc_size] = rounded_alloc(result_size);

	trivially_copy_values(result_alloc, lhs_begin, lhs_size);
	trivially_copy_values(result_alloc + lhs_size, rhs_begin, rhs_size);

	return string[ result_alloc, result_alloc + result_size, result_alloc + result_alloc_size ];
}

export operator + (lhs: char, rhs: #string) -> string
{
	let lhs_encoded = encode_char_utf8(lhs);
	let lhs_begin = lhs_encoded[0].begin();
	let lhs_size = lhs_encoded[1] as usize;
	let rhs_begin = rhs.begin_ptr();
	let rhs_size = rhs.size();

	let result_size = lhs_size + rhs_size;

	let [result_alloc, result_alloc_size] = rounded_alloc(result_size);

	trivially_copy_values(result_alloc, lhs_begin, lhs_size);
	trivially_copy_values(result_alloc + lhs_size, rhs_begin, rhs_size);

	return string[ result_alloc, result_alloc + result_size, result_alloc + result_alloc_size ];
}


export operator += (lhs: &mut string, rhs: #string) -> &mut string
{
	let rhs_size = rhs.size();
	let result_size = lhs.size() + rhs_size;
	lhs.reserve(result_size);

	// lhs.reserve could change rhs, if you write something like s += s
	trivially_copy_values(lhs.end_ptr(), rhs.begin_ptr(), rhs_size);
	lhs.set_size_unsafe(result_size);
	return lhs;
}

export operator += (lhs: &mut string, rhs: str) -> &mut string
{
	let rhs_size = rhs.size();
	let result_size = lhs.size() + rhs_size;
	lhs.reserve(result_size);

	trivially_copy_values(lhs.end_ptr(), rhs.begin_ptr(), rhs_size);
	lhs.set_size_unsafe(result_size);
	return lhs;
}

export operator += (lhs: &mut string, rhs: char) -> &mut string
{
	let rhs_encoded = encode_char_utf8(rhs);
	let rhs_begin = rhs_encoded[0].begin();
	let rhs_size = rhs_encoded[1] as usize;

	let result_size = lhs.size() + rhs_size;
	lhs.reserve(result_size);
	trivially_copy_values(lhs.end_ptr(), rhs_begin, rhs_size);
	lhs.set_size_unsafe(result_size);
	return lhs;
}


export operator == (lhs: #string, rhs: #string) -> bool
{
	return lhs.as_str() == rhs.as_str();
}

export operator == (lhs: str, rhs: #string) -> bool
{
	return lhs == rhs.as_str();
}

export operator == (lhs: #string, rhs: str) -> bool
{
	return lhs.as_str() == rhs;
}

export operator != (lhs: #string, rhs: #string) -> bool
{
	return lhs.as_str() != rhs.as_str();
}

export operator != (lhs: str, rhs: #string) -> bool
{
	return lhs != rhs.as_str();
}

export operator != (lhs: #string, rhs: str) -> bool
{
	return lhs.as_str() != rhs;
}


export function starts_with(s: #string, prefix: #string) -> bool
{
	return s.as_str().starts_with(prefix.as_str());
}

export function starts_with(s: #string, prefix: str) -> bool
{
	return s.as_str().starts_with(prefix);
}

export function ends_with(s: #string, postfix: #string) -> bool
{
	return s.as_str().ends_with(postfix.as_str());
}

export function ends_with(s: #string, postfix: str) -> bool
{
	return s.as_str().ends_with(postfix);
}
