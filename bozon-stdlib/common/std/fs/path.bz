import std::string;

export struct path
{
	._str: string;

	constructor(p: str)
	{
		return path[ string(p) ];
	}
}

export struct path_iterator
{
	._it: ?*const uint8;
	._path_end: ?*const uint8;
}

export operator * (it: path_iterator) -> str
{
	let end = it._it;
	const path_end = it._path_end;
	while (end != path_end && *end != '/' as uint32)
	{
		++end;
	}
	return __builtin_str_from_ptrs(it._it, end);
}

export operator ++ (it: &path_iterator) -> &path_iterator
{
	while (it._it != it._path_end && *it._it != '/' as uint8)
	{
		++it._it;
	}
	if (it._it != it._path_end)
	{
		++it._it;
	}
	return it;
}

export operator == (lhs: path_iterator, rhs: path_iterator) -> bool
{
	return lhs._it == rhs._it;
}

export operator != (lhs: path_iterator, rhs: path_iterator) -> bool
{
	return lhs._it != rhs._it;
}

export function begin(p: &const path) -> path_iterator
{
	return path_iterator[ p._str.data(), p._str.data_end() ];
}

export function end(p: &const path) -> path_iterator
{
	return path_iterator[ p._str.data_end(), p._str.data_end() ];
}

export function print(p: #const path)
{
	print(p._str);
}

export function println(p: #const path)
{
	println(p._str);
}

export function logln(p: #const path)
{
	__builtin_println_stderr(p._str.as_str());
}

export operator == (lhs: #const path, rhs: #const path) -> bool
{
	let lhs_it = lhs.begin();
	let rhs_it = rhs.begin();
	const lhs_end = lhs.end();
	const rhs_end = rhs.end();
	for (; lhs_it != lhs_end && rhs_it != rhs_end; ++lhs_it, ++rhs_it)
	{
		if (*lhs_it != *rhs_it)
		{
			return false;
		}
	}
	return lhs_it == lhs_end && rhs_it == rhs_end;
}

export operator != (lhs: #const path, rhs: #const path) -> bool
{
	return !(lhs == rhs);
}

export operator /= (lhs: &path, rhs: str) -> &path
{
	lhs._str ..= '/';
	lhs._str ..= rhs;
	return lhs;
}

export operator /= (lhs: &path, rhs: #const string) -> &path
{
	lhs._str ..= '/';
	lhs._str ..= rhs;
	return lhs;
}

export operator /= (lhs: &path, rhs: #const path) -> &path
{
	lhs._str ..= '/';
	lhs._str ..= rhs;
	return lhs;
}
